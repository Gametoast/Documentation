OffhandAnimation("stand_useforce", "FIRE")
{
    Loop("FinalFrame");
    AimType("Torso");
    BlendInTime(0.15);
    BlendOutTime(0.15);
}
OffhandAnimation("stand_useforce", "FIRE2");
OffhandAnimation("stand_useforce", "CHARGE");

State("IDLE")
{
    Duration(0.0);  // infinite duration
    EnergyRestoreRate();    // (0.0, "FromSoldier")
    InputLock(0.15, "Sprint", "Jump", "Crouch");

    Transition("DEFLECT")
    {
        If()
        {
            Break();    // all transitions are breaking if Duration is 0
            Posture("Stand", "Crouch");
            Energy(">=", 2.5);  // at least half a second worth of deflect time 
            Button("FireSecondary", "Press");
        }
    }
    Transition("BACK_ATTACK")
    {
        If()
        {
            Break();    // all transitions are breaking if Duration is 0
            Posture("Stand", "Crouch");
            Button("Fire", "Press");
	    Thrust(">", 0.25);
	    ThrustAngle(135, 225);	
        }
    }
    Transition("ATTACK1")
    {
        If()
        {
            Break();    // all transitions are breaking if Duration is 0
            Posture("Stand", "Crouch");
            Button("Fire", "Press");
        }
    }
    Transition("DASHATTACK")
    {
        If()
        {
            Break();    // all transitions are breaking if Duration is 0
            Posture("Sprint");
            Button("Fire", "Press");
        }
    }
    Transition("JUMPATTACK_FALL")
    {
        If()
        {
            Break();    // all transitions are breaking if Duration is 0
            Posture("Jump", "Jet");
            //The following should be true during a jetjump, jump, or fall 
            // unless we're more than 0.35 seconds from the apex.
            //NOTE: This may leave too little time for the jumpattack_fall anim
            // to play if darth is jetting very close to the ground, and may
            // not allow darth to jump attack during a long fall from high up.
            //VelocityY(">", -6.3);
            Button("Fire", "Press");
        }
    }
}

State("DEFLECT")
{
    Posture("Stand");
    Sound("imp_weap_lightsabre_block");
    Animation("stand_block_idle")
    {
        Loop();
        AimType("None");
        BlendInTime(0.15);
        BlendOutTime(0.15);
        LowResPose(0, "Frames");
    }

    Duration(0.0);  // infinite duration, looping anim

    InputLock(0.0, "All", "!Thrust");   // lock all but move controller for duration of state
    EnergyRestoreRate(-10.0);   // drain energy while ready to deflect
    Deflect()
    {
        DeflectAngle(-60, 60);  // deflect anything from forward arc
        EnergyCost(0.0);

        DeflectAnimation("stand_block_front1", "Forward")
        {
            // AimType("Torso");
            BlendInTime(0.00);
            BlendOutTime(0.05);
            LowResPose(5, "Frames");
        }
        DeflectAnimation("stand_block_front2", "Forward")
        {
            // AimType("Torso");
            BlendInTime(0.00);
            BlendOutTime(0.05);
            LowResPose(5, "Frames");
        }
        DeflectAnimation("stand_block_left1", "Left")
        {
            // AimType("Torso");
            BlendInTime(0.00);
            BlendOutTime(0.05);
            LowResPose(5, "Frames");
        }
        DeflectAnimation("stand_block_left2", "Left")
        {
            // AimType("Torso");
            BlendInTime(0.00);
            BlendOutTime(0.05);
            LowResPose(5, "Frames");
        }
        DeflectAnimation("stand_block_right1", "Right")
        {
            // AimType("Torso");
            BlendInTime(0.00);
            BlendOutTime(0.05);
            LowResPose(5, "Frames");
        }
        DeflectAnimation("stand_block_right2", "Right")
        {
            // AimType("Torso");
            BlendInTime(0.00);
            BlendOutTime(0.05);
            LowResPose(5, "Frames");
        }
    }

    Transition("ATTACK1")
    {
        If()
        {
            Break();    // all transitions are breaking if Duration is 0 
            Button("Fire", "Press");
        }
    }
    Transition("DEFLECT_ROLL") // same as the code checking for rolling
    {
        EnergyCost(25.0);
        If()
        {
            Break();    // all transitions are breaking if Duration is 0 
            Button("Crouch", "Press");
	    Thrust(">", 0.9);
            ThrustAngle(-120, 120);
        }
    }
    Transition("DEFLECT_CROUCH")
    {
        If()
        {
            Break();    // all transitions are breaking if Duration is 0 
            Button("Crouch", "Press");
        }
    }
    Transition("DEFLECT_JUMP")
    {
        If()
        {
            Break();    // all transitions are breaking if Duration is 0 
            Button("Jump", "Press");
        }
    }
    Transition("DEFLECT_SPRINT")
    {
        If()
        {
            Break();    // all transitions are breaking if Duration is 0 
            Button("Sprint", "Down");
        }
    }
    Transition("IDLE")
    {
        If()
        {
            Break();
            Energy("<=", 0.0);          // if no energy left
        }
        Or()
        {
            Break(20, "Frames");    // all transitions are breaking if Duration is 0 
            Button("FireSecondary", "Press");
        }
        Or()
        {
            Break();    // all transitions are breaking if Duration is 0 
            Button("Reload", "Press");
        }
    }
}

State("DEFLECT_JUMP")
{
    Posture("Jump");

    MustShowOneFrame();

    Transition("IDLE")
    {
        If()
        {
            Break(1, "Frames");
        }
    }
}

State("DEFLECT_CROUCH")
{
    Posture("Crouch");

    MustShowOneFrame();

    Transition("IDLE")
    {
        If()
        {
            Break(1, "Frames");
        }
    }
}

State("DEFLECT_ROLL")
{
    Posture("Roll");

    MustShowOneFrame();

    Transition("IDLE")
    {
        If()
        {
            Break(1, "Frames");
        }
    }
}

State("DEFLECT_SPRINT")
{
    Posture("Sprint");

    MustShowOneFrame();

    Transition("IDLE")
    {
        If()
        {
            Break(1, "Frames");
        }
    }
}

State("BACK_ATTACK")
{
    Posture("Stand");
    Animation("stand_attack_backwards")
    {	
	BlendInTime(0.15);
	BlendOutTime(0.15);
    }

    Sound("saber_double", 3, "Frames");

    Duration(35, "Frames");

    InputLock("All", "!Thrust");
    AlignedToThrust();
    Attack()
    {
        DamageTime(6, 10, "Frames");
        DamageLength(1.5);
        DamageWidth(0.75);
        Damage(300);
	Push(6.0);
    }

    Attack()
    {
        DamageTime(17, 22, "Frames");
        DamageLength(1.5);
        DamageWidth(0.75);
        Damage(300);
	Push(6.0);
    }

    Transition("IDLE");
}

//Forward declare animations to allow BlendTimeTo references:
Animation("stand_attack1a");
Animation("stand_attack1a_end");
Animation("stand_attack1b");
Animation("stand_attack1b_end");
Animation("stand_attack1c");

State("ATTACK1")
{
    Posture("Stand");
    Animation("stand_attack1a");
    Sound("saber_swing");

    InputLock("All", "!Thrust");
    AlignedToThrust();
    Attack()
    {
        DamageTime(2, 5, "Frames");
        DamageLength(1.5);
        DamageWidth(0.75);
        Damage(300);
	Push(3.0);
    }

    Transition("ATTACK2")
    {
        If()
        {
	    Break(14, "Frames");
            TimeStart(4, "Frames");
            TimeEnd(19, "Frames");
            Posture("Stand");
            Button("Fire", "Press");
        }
    }
    Transition("RECOVER1");
}

State("RECOVER1")
{
    Posture("Stand");
    Animation("stand_attack1a_end")
    {
//      SyncType("attack1b", "ByTime");
        AimType("None");
        BlendInTime(0.15);
        BlendOutTime(0.15);
    }

    InputLock("All", "!Thrust");
    AlignedToThrust();

    Transition("IDLE");
}

State("ATTACK2")
{
    Posture("Stand");
    Animation("stand_attack1b")
    {
//      SyncType("attack1b", "ByTime");
        AimType("None");
        BlendInTime(0.06);
        BlendOutTime(0.15);
        BlendTimeTo("stand_attack1b_end", 0.0);
        BlendTimeTo("stand_attack1c", 0.0);
    }
    Sound("saber_swing", 5, "Frames");

    InputLock("All", "!Thrust");
    AlignedToThrust();
    Attack()
    {
        DamageTime(7, 12, "Frames");
        DamageLength(1.5);
        DamageWidth(0.75);
        Damage(300);
	Push(3.0);
    }

    Transition("ATTACK3")
    {
        If()
        {
	    Break(20, "Frames");
            TimeStart(8, "Frames");
            TimeEnd(24, "Frames");   
            Posture("Stand");
            Button("Fire", "Press");
        }
    }
    Transition("IDLE");
}

State("ATTACK3")
{
    Posture("Stand");
    Animation("stand_attack1c")
    {
//      SyncType("attack1c", "ByTime");
        AimType("None");
        BlendInTime(0.06);
        BlendOutTime(0.15);
    }
    Sound("saber_attack_three");

    AnimatedMove()      // animation controls velocity for entire state
    {
        VelocityZ(0.0);
        VelocityX(0.0);
        VelocityFromThrust(12.0);
        VelocityFromStrafe(3.0);
    }
    AlignedToThrust();

    TurnFactor(0.5);

    Duration(14, "Frames");

    InputLock("All", "!Thrust");    // lock all controls for duration of state
    Attack()
    {
        DamageTime(2, 11, "Frames");
        DamageLength(1.5);
        DamageWidth(0.75);
        Damage(600);
	Push(8.0);
    }
    Attack()
    {
        DamageTime(11, 22, "Frames");
        DamageLength(1.5);
        DamageWidth(0.75);
        Damage(600);
	Push(8.0);
    }

    Transition("ATTACK3_LAND");
}

State("ATTACK3_LAND")
{
    Posture("Stand");
    Animation("stand_attack1c")
    {
//      SyncType("attack1c", "ByTime");
        AimType("None");
        BlendInTime(0.06);
        BlendOutTime(0.15);
    }

    AnimatedMove()      // animation controls velocity for entire state
    {
        VelocityZ(0.0);
        VelocityX(0.0);
        VelocityFromThrust(0.5);
        VelocityFromStrafe(0.5);
	Until()
	{
	    Break(25, "Frames");
	}
    }
    AlignedToThrust();

    TurnFactor(0.5);

    InputLock("All", "!Thrust");    // lock all controls for duration of state

    Attack()
    {
        DamageTime(11, 22, "Frames");
        DamageLength(1.5);
        DamageWidth(0.75);
        Damage(600);
	Push(8.0);
    }

    Transition("IDLE");
}

State("DASHATTACK")
{
    Posture("Sprint");
    Animation("stand_dashattack_upper")
    {
        AimType("FullBody");
    }
    Animation("stand_dashattack_lower")
    {
        AimType("FullBody");
    }

    Sound("saber_double", 4, "Frames");

    AnimatedMove()
    {
        VelocityZ(13.0);
        VelocityX(0.0);
        VelocityFromThrust(5.0);
        VelocityFromStrafe(1.0);
        If()
        {
            Break(20, "Frames");
        }
    }
    TurnFactor(0.3);

    InputLock("All", "!Thrust");
    Attack()
    {
        DamageTime(5, 11, "Frames");
        DamageLength(1.5);
        DamageWidth(0.75);
        Damage(400);
	Push(8.0);
    }

    Attack()
    {
        DamageTime(13, 18, "Frames");
        DamageLength(1.5);
        DamageWidth(0.75);
        Damage(400);
	Push(8.0);
    }

    Transition("IDLE");
    {
        If()
        {
            Break(30, "Frames");
            ButtonsPressed("Any");
        }
    }
}

// forward declare jumpattack_land, since jumpattack_fall refers to it:
Animation("jumpattack_land");

State("JUMPATTACK_FALL")
{
    Posture("Jump");

    Animation("jumpattack_fall")
    {
        Loop("FinalFrame");
        AimType("None");
        BlendInTime(0.15);
        BlendOutTime(0.15);
//      BlendTimeTo("jumpattack_land", 0.15);
    }

    Sound("com_weap_throw");

    Duration(0);

    EnergyRestoreRate(0.0);

    Gravity(4.0);
    GravityVelocityTarget(-15.0);   // accelerate us 
    AnimatedMove()
    {
        VelocityFromThrust(90.0);
        VelocityFromStrafe(0.0);
    }

    InputLock("All", "!Thrust");    // lock all buttons for duration of state

    AlignedToThrust();

    Transition("JUMPATTACK_LAND")
    {
        If()
        {
            Break();
            Posture("Stand");
        }
    }
    Transition("IDLE")
    {
        If()
        {
            Break();
            Posture("Any", "!Stand", "!Jump", "!Jet");
        }
    }
}

State("JUMPATTACK_LAND")
{
    Posture("Stand");

    Sound("saber_swing");

    Animation("jumpattack_land")
    {
        AimType("FullBody");
        //SyncType("jumpattack_land", "ByTime");
        BlendInTime(0.00);
        BlendOutTime(0.20);
    }

    Attack()
    {
        DamageTime(0, 8, "Frames");
        DamageLength(1.75);
        DamageWidth(0.75);
	Damage(800);
	Push(10.0);
    }

    EnergyRestoreRate(0.0);

    AnimatedMove()
    {
        VelocityZ(0.0);
        VelocityX(0.0);
        VelocityFromThrust(0.5);
        VelocityFromStrafe(0.5);
    }
    TurnFactor(0.3);

    InputLock("All", "!Thrust");

    Transition("JUMPATTACK_RECOVER");
}

State("JUMPATTACK_RECOVER")
{
    Posture("Stand");

    Animation("jumpattack_recover")
    {
        //SyncType("jumpattack_land", "ByTime");
        BlendInTime(0.00);
        BlendOutTime(0.20);
    }
 
    EnergyRestoreRate(0.0);

    TurnFactor(0.3);

    InputLock("All", "!Thrust");

    Transition("IDLE");
}